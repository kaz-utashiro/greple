#!/usr/bin/perl
##
## greple: grep with multiple keywords
##
## Copyright (c) 1991-2014 Kazumasa Utashiro
##
## Original: Mar 29 1991
;; my $rcsid = q$Id: greple,v 6.20 2014/01/13 05:55:59 utashiro Exp $;
##
## Use and redistribution for ANY PURPOSE are granted as long as all
## copyright notices are retained.  Redistribution with modification
## is allowed provided that you make your modified version obviously
## distinguishable from the original one.  THIS SOFTWARE IS PROVIDED
## BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES ARE
## DISCLAIMED.
##

use strict;
use warnings;

require 5.010;

use File::stat;
use IO::Handle;
use Getopt::Long qw(GetOptionsFromArray GetOptionsFromString);
use Pod::Usage;
use Text::ParseWords qw(shellwords);
use List::Util qw(min max first);
use Data::Dumper;

use utf8;
use Encode;
use Encode::Guess;


=head1 NAME


greple - grep with multiple keywords


=head1 SYNOPSIS


B<greple> [B<-M>I<module>] [ B<-options> ] pattern [ file... ]

    pattern           'positive -negative ?alternative'

    -e pattern        regex pattern match across line boundary
    -v pattern        regex pattern not to be matched
    --le pattern      lexical expression (same as bare pattern)
    --re pattern      regular expression
    --fe pattern      fixed expression

B<OPTIONS>

    -i                   ignore case
    -l                   list filename only
    -c                   print count of matched block only
    -n                   print line number
    -h                   do not display filenames
    -H                   always display filenames
    --inside=pattern     select matches inside of pattern
    --outside=pattern    select matches outside of pattern
    --include=pattern    reduce matches to the area
    --exclude=pattern    reduce matches to outside of the area
    --strict             strict mode for --inside/outside --block
    --join               delete newline in the matched part
    --joinby=string      replace newline in the matched text by string
    --filestyle=style    how filename printed (once, separate, line)
    --linestyle=style    how line number printed (separate, line)
    --separate           set filestyle and linestyle both "separate"

    --need=n             required positive match count
    --allow=n            acceptable negative match count

    --color=when         use terminal color (auto, always, never)
    --nocolor            same as --color=never
    --colormode=mode     R, G, B, C, M, Y, W, Standout, bolD, Underline
    --colorful           same as --colormode 'RD GD BD CD MD YD'
    --random             random color
    --regioncolor        color by region

    -o                   print only the matching part
    -p                   paragraph mode
    -A[n]                after match context
    -B[n]                before match context
    -C[n]                after and before match context
    --all                print whole data
    --block=pattern      specify the block of records
    --blockend=s         specify the block end mark (Default: "--\n")

    -f file              file contains search pattern
    -d flags             display info (f:file d:dir c:count m:misc s:stat)
    --man                display command or module manual page
    --show               display module file
    --icode=name         specify file encoding
    --ocode=name         specify output encoding
    --if=filter          set filter command
    --of=filter          output filter command
    --noif               disable default input filter
    --[no]pgp            decrypt and find PGP file (Default: false)
    --pgppass=phrase     pgp passphrase
    --readlist           get filenames from stdin
    --chdir              change directory before search
    --glob=glob          glob target files
    --print=func         print function
    --continue           continue after print function
    --call=func          call function before search
    --norc               skip reading startup file

=cut

my @opt_e;
my @opt_v;
my @opt_re;
my @opt_le;
my @opt_fe;
my @opt_or;

my @opt_if;
my @opt_of;
my @opt_glob;
my @opt_icode;
my @opt_block;
my @opt_inside;
my @opt_outside;
my @opt_include;
my @opt_exclude;
my @opt_chdir;
my @opt_call;
my $opt_noif;
my $opt_all;
my $opt_blockend;
my $opt_color = 'auto';
my $opt_colormode = 'RD';
my @opt_addcolor;
my $opt_colorfull;
my $opt_random;
my $opt_regioncolor;
my $opt_icode;
my $opt_ocode;
my $opt_man;
my $opt_show;
my $opt_pgp;
my $opt_pgppass;
my $opt_passphrase_fd;
my $opt_join;
my $opt_joinby = "";
my $opt_clean;
my $opt_readlist;
my $opt_rc = 1;
my $opt_need = 0;
my $opt_allow = 0;
my $opt_strict = 0;
my @opt_print;
my $opt_continue;
my $opt_filestyle = 'line';
my $opt_linestyle = 'line';

my $opt_c;
my @opt_d = ();
my @opt_f;
my $opt_h;
my $opt_i;
my $opt_n;
my $opt_o;
my $opt_p;
my $opt_l;
my $opt_A = 0;
my $opt_B = 0;
my $opt_C = 0;
my $opt_H;
#my $opt_M;

my @optargs = (
    'e|and=s'            => \@opt_e,
    'v|not=s'            => \@opt_v,
    'le=s'               => \@opt_le,
    're=s'               => \@opt_re,
    'fe=s'               => \@opt_fe,
    'need=i'             => \$opt_need,
    'allow=i'            => \$opt_allow,
    'i|ignore-case'      => \$opt_i,
    'l'                  => \$opt_l,
    'c|count'            => \$opt_c,
    'n|line-number'      => \$opt_n,
    'h|no-filename'      => \$opt_h,
    'H'                  => \$opt_H,
    'filestyle|fs=s'     => \$opt_filestyle,
    'linestyle|ls=s'     => \$opt_linestyle,
    'separate',          => sub { $opt_filestyle = $opt_linestyle = $_[0] },
    'o|only-matching!'   => \$opt_o,
    'p|paragraph!'       => \$opt_p,
    'A|after-context:2'  => \$opt_A,
    'B|before-context:2' => \$opt_B,
    'C|context:2'        => \$opt_C,
    'all'                => \$opt_all,
    'f|file=s'           => \@opt_f,
    'rc!'                => \$opt_rc,
    'readlist'           => \$opt_readlist,
    'd=s'                => \@opt_d,
    'man'                => \$opt_man,
    'show'               => \$opt_show,
    'icode=s'            => \@opt_icode,
    'ocode=s'            => \$opt_ocode,
    'join!'              => \$opt_join,
    'joinby=s'           => \$opt_joinby,
    'if=s'               => \@opt_if,
    'of=s'               => \@opt_of,
    'noif'               => \$opt_noif,
    'pgp!'               => \$opt_pgp,
    'pgppass=s'          => \$opt_pgppass,
    'block=s'            => \@opt_block,
    'blockend:s'         => \$opt_blockend,
    'inside=s'           => \@opt_inside,
    'outside=s'          => \@opt_outside,
    'include=s'          => \@opt_include,
    'exclude=s'          => \@opt_exclude,
    'strict!'            => \$opt_strict,
    'chdir=s'            => \@opt_chdir,
    'glob=s'             => \@opt_glob,
    'call=s'             => \@opt_call,
    'clean!'             => \$opt_clean,
    'quote=s'            => sub {$opt_color='always'; $opt_colormode=$_[1]},
    'color|colour=s'     => \$opt_color,
    'colormode|cm|colourmode:s'
                         => \$opt_colormode,
    'addcolor=s'         => \@opt_addcolor,
    'nocolor|nocolour'   => sub {$opt_color = 'never'},
    'colorful'           => sub {$opt_colormode = 'RD GD BD CD MD YD'},
    'random!'            => \$opt_random,
    'regioncolor|rc!'    => \$opt_regioncolor,
    'print=s'            => \@opt_print,
    'continue!'          => \$opt_continue,
    'M|module:s'         => sub { warn "Use -M option before other options.\n"; exit 2 },
);

binmode STDERR, ":encoding(utf8)";

sub add_option {
    push @optargs, @_;
}

##
## Process special option --norc
##
if (@ARGV and $ARGV[0] eq "--norc") {
    $opt_rc = 0;
    shift @ARGV;
}

##
## User customizable option handling
##
my @rcdata;

##
## Read .greplerc file
##
if ($opt_rc &&
    open(GREPLERC, "<:encoding(utf8)", "$ENV{HOME}/.greplerc")) { # XXX
    my $rc = do { local $/; <GREPLERC> };
    greplerc({Title => "main"}, $rc);
    close GREPLERC;
}

sub greplerc {
    my $arg = ref $_[0] eq 'HASH' ? shift : {};
    my $rc = join '', grep(/\S/, @_);
    for ($rc) {
	s/^__CODE__\s*\n(.*)//ms and do_eval($1);
	s/^\s*(?:#.*)?\n//mg;
	s/\\\n//g;
    }
    push @rcdata, new RcData($arg->{Title})->parsetext($rc);
}

##
## Process special options before using Getopt.
##
while (@ARGV) {
    if ($ARGV[0] =~ /^-M(.*)/) {
	shift @ARGV;
	my $module = $1 || shift or do {
	    warn "Option M requires an argument.\n";
	    if (my @modules = uniq(module_list())) {
		warn "\nAvailable modules:\n";
		warn "\t", join("\n\t", @modules), "\n";
	    }
	    exit -1;
	};
	load_module($module);
	next;
    }
    last;
}
sub load_module {
    my $mod = shift;
    my $module = $mod;
    my $base = "App::Greple";
    my $call;

    ##
    ## Check -Mmod::func(arg) or -Mmod::func=arg
    ##
    if ($module =~ s{
	^ (?<name> .* ) ::
	  (?<call>
		\w+
		(?: (?<P>[(]) | = )  ## start with '(' or '='
		(?<arg> [^)]* )      ## optional arg list
		(?(<P>) [)] | )      ## close ')' or none
	  ) $
    }{$+{name}}x) {
	$call = $+{call};
    }

    if (-f $module) {
	require $module;
    } else {
	$module = "${base}::${module}";
	eval "use $module";
    }
    if ($@) {
	die "$mod: Module error ($@)\n";
    }
    local *data = "${module}::DATA";
    if (not eof *data) {
	greplerc({Title => $mod}, <data>);
    }
    if ($call) {
	push @opt_call, "${module}::${call}";
    }
}
sub module_list {
    map { /(\w+)\.pm$/ }
    map { glob "$_/App/Greple/*.pm" }
    @INC;
}

##
## Handle default option and ENG{GREPLEOPTS}
##
unshift @ARGV, map { $_->default() } @rcdata;
if ($ENV{'GREPLEOPTS'}) {
    unshift @ARGV, shellwords($ENV{'GREPLEOPTS'});
}

##
## Process user defined option.
##
ARGV: for (my $i = 0; $i < @ARGV; $i++) {
    my $arg = $ARGV[$i];
    for my $rcdata (@rcdata) {
	if (my @s = $rcdata->getopt($arg)) {
	    splice @ARGV, $i, 1, @s;
	    redo ARGV;
	}
    }
}

##
## Decode @ARGV
##
foreach (@ARGV) {
    $_ = decode 'utf8', $_ unless utf8::is_utf8($_); # XXX
}

my @SAVEDARGV = @ARGV;
Getopt::Long::Configure("bundling");
GetOptions(@optargs) || pod2usage;

sub set_options {
    GetOptionsFromArray(\@_, @optargs) || pod2usage;
}

sub set_options_fromstring {
    my $string = shift;
    GetOptionsFromString($string, @optargs) || pod2usage;
}

my %opt_d;
@opt_d = split(//, join('', @opt_d));
@opt_d{@opt_d} = @opt_d;

if ($opt_d{o}) {
    warn "\@ARGV = ", join(' ', shellquote(@SAVEDARGV)), "\n";
}

my $file_code;
my $default_icode = 'utf8';	# default input encoding
my @default_icode_list = qw(euc-jp 7bit-jis);
my $output_code;
my $default_ocode = 'utf8';	# default output encoding

$output_code = $opt_ocode || $default_ocode;
binmode STDOUT, ":encoding($output_code)";

## show unused option characters
if ($opt_d{u}) {
    my $s = join('','0'..'9',"\n",'a'..'z',"\n",'A'..'Z',"\n");
    map { /^([0-9a-zA-Z])(?:\|[^=]+)*(?:=[is])?$/ && $s =~ s/$1/./ } @optargs;
    die $s;
}

## show man pages
if ($opt_man or $opt_show) {
    if (@rcdata > 1 and (my $mod = $rcdata[-1]->title) ne "main") {
	my @perldocopt = $opt_show ? '-m' : () ;
	my $jp = first { -x "$_/perldocjp" } split /:/, $ENV{PATH};
	my $perldoc = $jp ? "perldocjp" : "perldoc";
	exec "$perldoc @perldocopt App::Greple::$mod";
    }
    pod2usage({-verbose => 2});
    die;
}

## setup file encoding
if (@opt_icode) {
    @opt_icode = map { split /[,\s]+/ } @opt_icode;
    if (grep { s/^\+// } @opt_icode) {
	unshift @opt_icode, @default_icode_list;
    }
    @opt_icode = uniq(@opt_icode);
    if (@opt_icode > 1) {
	@opt_icode = grep { !/(?:auto|guess)$/i } @opt_icode;
	Encode::Guess->set_suspects(@opt_icode);
	$file_code = 'Guess';
    }
    elsif ($opt_icode[0] =~ /^(?:guess|auto)$/i) {
	Encode::Guess->set_suspects(@default_icode_list);
	$file_code = 'Guess';
    } else {
	$file_code = $opt_icode[0];
    }
}
else {
    $file_code = $default_icode;
}

## make search pattern
if (@opt_f) {
    my @patterns;
    for my $opt_f (@opt_f) {
	open my $fh, '<:encoding(utf8)', $opt_f or die "$opt_f: $!\n";
	push @patterns, do {
	    map  { chomp ; s{\s*//.*}{} ; $_ }
	    grep { not m{^\s*(?:#|//|$)} }
	    <$fh>
	} ;
	close $fh;
    }
    unshift @opt_re, join('|', map { make_pattern($_) } @patterns);
} else {
    if (@opt_le + @opt_re + @opt_fe + @opt_or + @opt_e == 0) {
	&usage if @ARGV == 0;
	unshift @opt_le, shift(@ARGV);
    }
}

##
## --le
##
for (@opt_le) {
    for my $p (split /(?<!\\) +/, $_) {
	next if $p eq "";
	if ($p =~ s/^-//) {		# -pattern
	    push @opt_v, $p;
	} elsif ($p =~ s/^\?//) {	# ?pattern
	    push @opt_or, $p;
	} elsif ($p =~ s/^\+// or 1) {	# +pattern
	    push @opt_e, $p;
	}
    }
}

my @xpattern;
use constant {
    MATCH_POSITIVE => 1,
    MATCH_NEGATIVE => 0,
};

for my $p (@opt_e) {
    $p = make_pattern($p);
    push @xpattern, [MATCH_POSITIVE, qr/$p/m];
}
for my $p (@opt_v) {
    $p = make_pattern($p);
    push @xpattern, [MATCH_NEGATIVE, qr/$p/m];
}
if (@opt_or) {
    my $p = join('|', map { &make_pattern($_) } @opt_or);
    push @xpattern, [MATCH_POSITIVE, qr/$p/m];
}

for my $p (@opt_re) {
    push @xpattern, [MATCH_POSITIVE, qr/$p/m];
}
for my $p (@opt_fe) {
    push @xpattern, [MATCH_POSITIVE, qr/\Q$p/];
}

my($positive_pattern, $negative_pattern);
for (@xpattern) {
    $_->[0] == MATCH_POSITIVE ? $positive_pattern++ : $negative_pattern++;
}

$opt_need += $positive_pattern if $opt_need <= 0;
$opt_allow += $negative_pattern if $opt_allow < 0;

##
## setup input/output filter
##
my @filter;

unless ($opt_noif) {
    push(@filter,
	 [ sub { s/\.Z$//   }, 'zcat' ],
	 [ sub { s/\.g?z$// }, 'gunzip -c' ],
	 [ sub { m/\.pdf$/i }, 'pdftotext -nopgbrk - -' ],
	);
}
for my $if (@opt_if) {
    my @f = split /:/, $if;
    while (@f) {
	if (@f > 1) {
	    push @filter, [ splice(@f, 0, 2) ];
	} else {
	    push @filter, [ 1, shift(@f) ];
	}
    }
}

sub filter {
    my $file = shift;
    local($_) = $file;

    my @f;
    for (my $remember = ""; $remember ne $_; ) {
	$remember = $_;
	for my $p (@filter) {
	    my($exp, $command) = @$p;
	    if (ref $exp eq 'CODE' ? &$exp : eval $exp) {
		push @f, $command;
	    }
	}
    }

    my $filter = join " | ", @f;
    $filter =~ s/{}/$file/g;
    $filter;
}

my $pgp;
if ($opt_pgp) {
    $pgp = new PgpFilter;
    if ($opt_pgppass) {
	$pgp->initialize({passphrase => $opt_pgppass});
    } elsif ($opt_passphrase_fd) {
	$pgp->initialize({passphrase_fd => $opt_passphrase_fd});
    } else {
	$pgp->initialize();
    }
    push @filter, [ sub { s/\.(pgp|gpg|asc)// }, $pgp->_decrypt_command ];
}

# open output filter
if (@opt_of) {
    my $of = join ' | ', @opt_of;
    open(STDOUT, "| $of") || die "$of: $!\n";
    $| = 1;
    if ($opt_d{m}) {
	warn "Output Filter:\n  \"$of\"\n";
    }
}

##------------------------------------------------------------
## miscellaneous setups
##

my @argv_files;
my $start_directory;
my $need_filename = ($opt_H or $opt_l);
my $current_file;

use Cwd;
if (@opt_chdir) {
    $start_directory = getcwd;
    @opt_chdir = map { glob $_ } @opt_chdir;
    push @argv_files, splice(@ARGV);
    unless ($opt_h or
	    (@opt_chdir == 1 and @argv_files == 1 and @opt_glob == 0)) {
	$need_filename++;
    }
}
elsif (@opt_glob) {
    push @ARGV, map(glob, @opt_glob);
}

open(SAVESTDIN, '<&STDIN');
push(@ARGV, '-') unless @ARGV || @argv_files || @opt_glob || $opt_readlist;
if ((@ARGV > 1 or $opt_readlist) and not $opt_h) {
    $need_filename++;
}

$opt_join = 1 if $opt_joinby ne "";

if (@opt_addcolor) {
    $opt_colormode = "@opt_addcolor";
}
my @colors = map { [ colorseq($_), $_ ] } split(' ', $opt_colormode);

my $need_color = (($opt_color eq 'always')
		  or (($opt_color eq 'auto') and (!$opt_o and -t STDOUT)));

my $blockend = "--\n";
if (defined $opt_blockend) {
    ($blockend = $opt_blockend) =~ s/(?<=.)$/\n/;
}
if ($opt_C) {
    $opt_A ||= $opt_C;
    $opt_B ||= $opt_C;
}
my %stat = (
    files => 0,
    match_effective => 0,
    match_positive => 0,
    match_negative => 0,
    match_block => 0,
    time_start => [],
    time_end => [],
    );

##
## Setup functions
##
for my $set (
    [ "print"  , \@opt_print  , 0 ],
    [ "call"   , \@opt_call   , 0 ],
    [ "block"  , \@opt_block  , 1 ],
    [ "inside" , \@opt_inside , 1 ],
    [ "outside", \@opt_outside, 1 ],
    [ "include", \@opt_include, 1 ],
    [ "exclude", \@opt_exclude, 1 ],
    ) {
    my($cat, $opt, $pattern) = @$set;
    for (@{$opt}) {
	s/^&// or next if $pattern;
	$_ = parse_func($_) or die "$cat function format error: $_\n";
    }
}

my @reg_union =
    (map({ [ 1, $_ ] } @opt_inside), map({ [ 0, $_ ] } @opt_outside));

my @reg_clude =
    (map({ [ 1, $_ ] } @opt_include), map({ [ 0, $_ ] } @opt_exclude));

##------------------------------------------------------------

## patterns
if ($opt_d{m}) {
    warn "Search pattern:\n";
    for my $i (0 .. $#xpattern) {
	my($mode, $p) = @{$xpattern[$i]};
	warn sprintf("  %s /%s/\n", $mode ? 'and' : 'not',
		     @colors > 1 ? color($p, $i) : $p);
    }
}

## record start time
if ($opt_d{s}) {
    $stat{time_start} = [times];
}

$SIG{'QUIT'} = 'QUIT';
sub QUIT { die "Interrupted\n"; }
MAIN: {
    eval { &main };
    if ($@) {
	if ($@ =~ /Interrupted/) {	# interrupted
	    ;
	} else {
	    warn "$current_file: $@";
	}
	close STDIN; # wait;
	open STDIN, '<&SAVESTDIN';
	redo MAIN;
    }
}

## show statistic info
if ($opt_d{s}) {
    $stat{time_end} = [times];
    my @s = @{$stat{time_start}};
    my @e = @{$stat{time_end}};
    printf(STDERR "cpu %.3fu %.3fs\n", $e[0]-$s[0], $e[1]-$s[1]);
    for my $k (sort keys %stat) {
	my $v = $stat{$k};
	print STDERR "$k: ";
	if (ref $v eq 'ARRAY') {
	    local $" = ", ";
	    print STDERR "(@$v)";
	} else {
	    print STDERR $v;
	}
	print STDERR "\n";
    }
}

close STDOUT;

if ($opt_d{p}) {
    open STDOUT, ">&STDERR";
    system "ps -lww -p $$";
}

exit($stat{match_effective} == 0);

######################################################################

sub main {
    while (defined($current_file = open_nextfile())) {
	my $matched = grep_file();
	no warnings 'uninitialized'; # why ?
	$stat{match_effective} += $matched;
	$stat{files}++;
    } continue {
	close STDIN; # wait;	# wait for 4.019 or earlier?
	# recover STDIN for opening '-' and some weird command which needs
	# STDIN opened (like unzip)
	open STDIN, '<&SAVESTDIN';
    }
}

sub usage {
    pod2usage(-verbose => 0, -exitval => "NOEXIT");

    my $quote = qr/[\\(){}\|\*?]/;
    for my $rcdata (@rcdata) {
	my $title = $rcdata->title;
	if ($title eq "main") {
	    print "    User defined options:\n";
	} else {
	    print "    Module $title options:\n";
	}
	for my $name ($rcdata->options) {
	    my $help = $rcdata->help($name) || "";
	    next if $help eq 'ignore';
	    my @option = $rcdata->getopt($name, DEFAULT => 1);
	    printf("        %-20s %s\n", $name,
		   $help || join(' ', shellquote(@option)));
	}
	print "\n";
    }

    print "$rcsid\n" if $rcsid =~ /:/;

    exit 2;
}

# easy implementation. don't be serious.
sub shellquote {
    my $quote = qr/[\s\\(){}\|\*?]/;
    map { /^(-+\w+=)(.*$quote.*)$/
	      ? "$1\'$2\'"
	      :  /^(.*$quote.*)$/
	      ? "\'$1\'"
	      : $_ }
    @_;
}

sub make_pattern {
    my($p) = @_;

    my $wchar_re = qr{
	[\p{East_Asian_Width=Wide}\p{East_Asian_Width=FullWidth}]
    }x;

    if ($p =~ s/^\\Q//) {
	$p = quotemeta($p);
    } else {
	$p =~ s{
	    (
	     \[[^\]]*\]		# character-class
	     |
	     \(\?\<[=!][^\)]*\)	# look-behind pattern
	    )
	    |
	    ($wchar_re+)
	    |
	    (\w+|.)
	}{
	    if (defined $1) {
		$1;
	    } elsif (defined $2) {
		join('\s*', split(//, $2));
	    } elsif (defined $3) {
		$3;
	    } else {
		die;
	    }
	}egx;

	# ( [
	$p =~ s/($wchar_re)([\(\[]+)/$1\\s*$2/g;

	# ) ]
	$p =~ s/($wchar_re[\)\]]+)(?!\||$)/$1\\s*/g;

	# remove \s* arround space
	$p =~ s/(?:\\s\*)?\\? +(?:\\s\*)?/\\s+/g;
    }

    length($p) ? ($opt_i ? "(?i)$p" : $p) : undef;
}

sub open_nextfile {

    ##
    ## --chdir
    ##
    while (@ARGV == 0 and @opt_chdir and (@argv_files or @opt_glob)) {
	my $dir = shift @opt_chdir;
	warn "chdir $dir/\n" if $opt_d{d};
	chdir $start_directory or die "$!: $start_directory\n";
	chdir $dir or die "$!: $dir\n";
	push(@ARGV, @argv_files, map(glob, @opt_glob));
    }

    my $file;
    while (defined($file = shift(@ARGV)) ||
	   defined($file = $opt_readlist && <SAVESTDIN>)) {
	$file =~ s/\n+$//;

	my $file_st = stat($file);
	if (-p _ || -c _ || -b _) {	# skip FIFO and device files
	    next;
	}

	if (0) {}
	elsif ($file =~ /^http:\/\//) {
	    open(STDIN, '-|') || exec("w3m -dump $file") || die "w3m: $!\n";
	}
	else {
	    open(STDIN, $file) or do {
		warn "$file: $!\n" unless -l $file;
		next;
	    };
	}

	my $input_filter;
	if (defined(&filter) && ($input_filter = &filter($file))) {
	    open(STDIN, '-|') || exec($input_filter)
		|| die "$input_filter: $!\n";
	    if ($opt_d{m}) {
		printf(STDERR "Input Filter:\n  \"$input_filter\" < %s\n",
		       $file);
	    }
	}

	binmode STDIN, ":encoding($file_code)";

	return $file;
    }
    undef;
}

######################################################################

sub grep_file {
    my $content = do { local $/; <STDIN> };
    $pgp->reset if $pgp;

    warn $current_file, ":\n" if $opt_d{f};

    defined $content or return 0;

    for my $f (@opt_call) {
	for ($content) {
	    callfunc(@$f, file => $current_file);
	}
    }

    my $matched = do_grep($current_file, \$content);
    if ($opt_clean) {
	$content =~ s/./\000/gs;
    }

    $matched;
}

sub do_grep {
    my $file = shift;
    local *_ = shift;
    my @result = grep_pattern(\$_, \@xpattern);

    my $matched_count = 0;
    map { $matched_count += @{$_} - 1 } @result;

    if ($opt_l) {
	print "$current_file\n" if $matched_count;
	return $matched_count;
    }

    if ($opt_c) {
	printf "%s%d\n", $need_filename ? "$file:" : "", scalar(@result);
	return $matched_count;
    }

    if ($matched_count == 0) {
	return $matched_count;
    }

    if ($need_filename and $opt_filestyle eq 'once') {
	print $current_file, ":\n";
    }

    my $need_blockend =
	$opt_blockend || $opt_p || $opt_A || $opt_B || @opt_block;

    my $line = 1;
    my $lastpos = 0;
    for my $result (@result) {

	my($block_start, $block_end) = @{shift @$result};
	my $block = substr($_, $block_start, $block_end - $block_start);
	my $need_newline = not $block =~ /\n\Z/;

	for (@$result) {
	    $_->[0] = $_->[0] > $block_start ? $_->[0] - $block_start : 0;
	    $_->[1] = $_->[1] - $block_start;
	}

	for my $func (@opt_print) {
	    for ($block) {
		$_ = callfunc(@$func, file => $file, matched => $result);
	    }
	}
	if (@opt_print and not $opt_continue) {
	    print $block if defined $block;
	    next;
	}

	if ($opt_n) {
	    my $gap = substr($_, $lastpos, $block_start - $lastpos);
	    $line += $gap =~ tr/\n/\n/;
	}
	$lastpos = $block_end;

	if (my $nl = 0, 1) {
	    if ($need_filename and $opt_filestyle eq 'separate') {
		print $current_file, ":";
		$nl++;
	    }
	    if ($opt_n and $opt_linestyle eq 'separate') {
		print $line, ":";
		$nl++;
	    }
	    print "\n" if $nl;
	}

	my $mark = "\001";
	for my $matched (reverse @$result) {
	    my($ms, $me, $pi) = @$matched;
	    my $s = substr($block, $ms, $me - $ms);

	    $pi = rand(@colors) if $opt_random;

	    if ($opt_join) {
		if ($opt_n and $opt_linestyle eq 'line') {
		    $s =~ s/\n/$mark/g;
		} else {
		    $s =~ s/\n/$opt_joinby/g;
		}
	    }

	    if ($s ne '' and $need_color) {
		substr($block, $ms, $me - $ms) = color($s, $pi);
	    }
	}
	if ($opt_n and $opt_linestyle eq 'line') {
	    my $increment = $block =~ /[\n$mark]/ ? 1 : 0;
	    $block =~ s{(?:($mark)|(?<=\n)|\A)(?=.)}{
			my $s = $1 ? $opt_joinby : sprintf("%d:", $line);
			$line += $increment;
			$s;
		       }gse;
	}

	if ($need_filename and $opt_filestyle eq 'line') {
	    $block =~ s/^/$file:/mg;
	}

	print $block;
	print "\n" if $need_newline;
	print $blockend if $need_blockend;
    }

    $matched_count;
}

sub color {
    my $text = shift;
    if (@colors) {
	my $index = @_ ? shift(@_) % @colors : 0;
	$text =~ s/([^\r\n]+)/$colors[$index][0]$1$colors[$index][1]/g;
    }
    $text;
}

use Term::ANSIColor qw(:constants);
sub colorseq {
    my $colormode = shift;

    my($start, $end) = ('', '');
    if ($colormode =~ /,/) {
	($start, $end) = split(/,/, $colormode, 2);
    } else {
	map {
	    $start .= UNDERLINE if /U/;
	    $start .= REVERSE   if /S/;
	    $start .= BOLD      if /D/;
	    $start .= BLINK     if /F/;
	    $start .= RED       if /R/; $start .= ON_RED       if /r/;
	    $start .= GREEN     if /G/; $start .= ON_GREEN     if /g/;
	    $start .= BLUE      if /B/; $start .= ON_BLUE      if /b/;
	    $start .= CYAN      if /C/; $start .= ON_CYAN      if /c/;
	    $start .= MAGENTA   if /M/; $start .= ON_MAGENTA   if /m/;
	    $start .= YELLOW    if /Y/; $start .= ON_YELLOW    if /y/;
	    $start .= BLACK     if /K/; $start .= ON_BLACK     if /k/;
	    $start .= WHITE     if /W/; $start .= ON_WHITE     if /w/;
	} $colormode if $colormode;
	$end = RESET if $start;
    }
    ($start, $end);
}

######################################################################

sub grep_pattern {
    local *_ = shift;
    my @patterns = @{+shift};
    my @blocks;

    ##
    ## build match result list
    ##
    my @result;
    my %required = (yes => 0, no => 0);
    for my $i (0 .. $#patterns) {
	my($sw, $regex) = @{$patterns[$i]};
	my $rp = $result[$i] = [];
	while ($_ =~ /$regex/g) {
	    push @$rp, [ int $-[0], int $+[0], $i ];
	    if ($sw eq MATCH_POSITIVE) {
		push @blocks, [ int $-[0], int $+[0] ];
		$stat{match_positive}++;
	    } else {
		$stat{match_negative}++;
	    }
	}
	if (@$rp) {
	    $required{$sw eq MATCH_POSITIVE ? 'yes' : 'no'}++;
	}
    }
    $stat{match_block} += @blocks;

    ##
    ## optimization for zero match
    ##
    return () if $required{yes} == 0;

    ##
    ## --inside, --outside
    ##
    if (@reg_union) {
	my @tmp = map { [] } @result;
	for my $regi (0 .. $#reg_union) {
	    my($region_sw, $arg) = @{$reg_union[$regi]};
	    my @select = get_regions(\$_, $arg);
	    next unless $region_sw or @select;
	    for my $resi (0 .. $#result) {
		my $r = $result[$resi];
		my @l = select_regions({ strict => $opt_strict },
				       $r, \@select, $region_sw);
		if ($opt_regioncolor or @result == 1) {
		    map { $_->[2] = $regi } @l;
		}
		push @{$tmp[$resi]}, @l;
	    }
	}
	@result = map { [ merge_regions({sort => 1}, @$_) ] } @tmp;
    }

    ##
    ## --include, --exclude
    ##
    for my $region (@reg_clude) {
	my($region_sw, $arg) = @$region;
	my @select = get_regions(\$_, $arg);
	next unless $region_sw or @select;
	for my $r (@result) {
	    @$r = select_regions({ strict => $opt_strict },
				 $r, \@select, $region_sw);
	}
    }

    ##
    ## --all
    ##
    if ($opt_all) {
	@blocks = ( [ 0, length $_ ] );
    }
    ##
    ## --block
    ##
    elsif (@opt_block) {
	@blocks = ();
	for my $re (@opt_block) {
	    push @blocks, get_regions(\$_, $re);
	}
    	@blocks = merge_regions({nojoin => 1, sort => 1}, @blocks);
    }
    ##
    ## build block list from matched range
    ##
    elsif (@blocks) {
	my $textp = \$_;
	my $sub_blocknize = optimized_blocknize();
    	@blocks =
	    merge_regions({nojoin => 1, sort => 1},
			  map({[$sub_blocknize->($textp, $_->[0], $_->[1])]}
			      @blocks));
    }

    ##
    ## build match table
    ##
    my @match_table = map { [ 0, 0, [], 0, 0, [] ] } @blocks;
    use constant {
	POSI_BASE => 0, POSI_POSI => 0, POSI_NEGA => 1, POSI_LIST => 2,
	NEGA_BASE => 3, NEGA_POSI => 3, NEGA_NEGA => 4, NEGA_LIST => 5,
			INDX_POSI => 0, INDX_NEGA => 1, INDX_LIST => 2,
    };
    for my $i (0 .. $#result) {
	my $base = $patterns[$i][0] eq MATCH_POSITIVE ? POSI_BASE : NEGA_BASE;
	my @b = classify_regions({ strict => $opt_strict },
				 $result[$i], \@blocks);
	for my $bi (0 .. $#b) {
	    my $te = $match_table[$bi];
	    if (@{$b[$bi]}) {
		${$te}[$base + INDX_POSI]++;
		push @{$te->[$base + INDX_LIST]}, @{$b[$bi]};
	    } else {
		${$te}[$base + INDX_NEGA]++;
	    }
	}
    }

    show_match_table(\@match_table) if $opt_d{v};

    ##
    ## now it is quite easy to get effective blocks
    ##
    my @effective_index = grep(
	$match_table[$_][POSI_POSI] >= $opt_need &&
	$match_table[$_][NEGA_POSI] <= $opt_allow, 0 .. $#blocks);

    ##
    ## --block with -ABC option
    ##
    if (@opt_block and ($opt_A or $opt_B)) {
	my %mark;
	for my $i (@effective_index) {
	    map($mark{$_} = 1,
		max($i - $opt_B, 0) .. min($i + $opt_A, $#blocks));
	}
	@effective_index = sort { $a <=> $b } map { int $_ } keys %mark;
    }

    ##
    ## compose the result
    ##
    my @list = ();
    for my $bi (@effective_index) {
	my @matched = merge_regions({nojoin => $opt_o, sort => 1},
				    @{$match_table[$bi][POSI_LIST]},
				    @{$match_table[$bi][NEGA_LIST]}
	    );
	if ($opt_o) {
	    push @list, map({ [ $_, $_ ] } @matched);
	} else {
	    push @list, [ $blocks[$bi], @matched ];
	}
    }

    ##
    ## ( [ [blockstart, blockend], [start, end], [start, end], ... ],
    ##   [ [blockstart, blockend], [start, end], [start, end], ... ], ... )
    ##
    return @list;
}

BEGIN {
    $Data::Dumper::Terse = 1;
}
sub show_match_table {
    my $i = 0;
    for my $t (@{+shift}) {
	my $m = Dumper($t);
	$m =~ s/\s+(?!$)/ /gs;
	printf STDERR "%d %s", $i++, $m;
    }
}

sub classify_regions {
    my $opt = ref $_[0] eq 'HASH' ? shift : {};

    $opt->{strict} and goto &classify_regions_strict;

    my @list = @{+shift};
    my @by = @{+shift};
    my @table;
    for (my $i = 0; $i < @by; $i++) {
	my($from, $to) = @{$by[$i]};
	while (@list and $list[0][1] < $from) {
	    shift @list;
	}
	while (@list and $list[0][1] == $from and $list[0][0] < $from) {
	    shift @list;
	}
	my $t = $table[$i] = [];
	for (my $i = 0; ($i < @list) and ($list[$i][0] < $to); $i++) {
	    push @$t, [ @{$list[$i]} ];
	}
    }
    @table;
}

sub classify_regions_strict {
    my @list = @{+shift};
    my @by = @{+shift};
    my @table;
    for (my $i = 0; $i < @by; $i++) {
	my($from, $to) = @{$by[$i]};
	while (@list and $list[0][0] < $from) {
	    shift @list;
	}
	my $t = $table[$i] = [];
	while (@list and $list[0][0] < $to and $list[0][1] <= $to) {
	    push @$t, shift @list;
	}
    }
    @table;
}

sub select_regions {
    my $opt = ref $_[0] eq 'HASH' ? shift : {};

    $opt->{strict} and goto &select_regions_strict;

    my @list = @{+shift};
    my @by = @{+shift};
    my $inside = shift;

    my(@outside, @inside);

    for (my $i = 0; $i < @by; $i++) {
	my($from, $to) = @{$by[$i]};
	for (my $j = 0; $j < @list and $list[$j][0] < $from; $j++) {
	    push @outside, [ @{$list[$j]} ];
	}
	while (@list and $list[0][1] < $from) {
	    shift @list;
	}
	for (my $j = 0; $j < @list and $list[$j][0] < $to; $j++) {
	    push @inside, [ @{$list[$j]} ];
	}
	while (@list and $list[0][1] < $to) {
	    shift @list;
	}
	while (@list and $list[0][0] < $to and $list[0][1] <= $to) {
	    shift @list;
	}
    }
    push @outside, @list;
    $inside ? @inside : @outside;
}

sub select_regions_strict {
    my @list = @{+shift};
    my @by = @{+shift};
    my $inside = shift;

    my(@outside, @inside);

    for (my $i = 0; $i < @by; $i++) {
	my($from, $to) = @{$by[$i]};
	while (@list and $list[0][0] < $from and $list[0][1] <= $from) {
	    push @outside, shift(@list);
	}
	while (@list and $list[0][0] < $from) {
	    shift @list;
	}
	while (@list and $list[0][0] < $to and $list[0][1] <= $to) {
	    push @inside, shift(@list);
	}
	while (@list and $list[0][0] < $to) {
	    shift @list;
	}
    }
    push @outside, @list;
    $inside ? @inside : @outside;
}

sub get_regions {
    local *_ = shift;
    my $pattern = shift;

    ## function array
    if (ref $pattern eq 'ARRAY') {
	callfunc(@$pattern, file => $current_file);
    }
    ## pattern
    else {
	matched_regions({catchgroup => 0}, \$_, $pattern);
    }
}

sub matched_regions {
    my $opt = ref $_[0] eq 'HASH' ? shift : {};
    local *_ = shift;
    my($pattern) = @_;
    my @list;

    my $re = qr/$pattern/m;
    while (/$re/g) {
	if ($opt->{catchgroup}) {
	    push @list, defined $-[1] ? [$-[1], $+[1]] : [$-[0], $+[0]];
	} else {
	    push @list, [$-[0], $+[0]];
	}
    }
    @list;
}

sub merge_regions {
    my $option = ref $_[0] eq 'HASH' ? shift : {};
    my $nojoin = $option->{nojoin};
    my @in = @_;
    if ($option->{sort}) {
	@in = sort({$a->[0] <=> $b->[0] || $b->[1] <=> $a->[1]
			||  (@$a > 2 ? $a->[2] <=> $b->[2] : 0)
		   } @in);
    }
    my @out;
    push(@out, shift @in) if @in;
    while (@in) {
	my $top = shift @in;

	if ($out[-1][1] > $top->[0]) {
	    $out[-1][1] = $top->[1] if $out[-1][1] < $top->[1];
	}
	elsif (!$nojoin
	       and $out[-1][1] == $top->[0]
	       ##
	       ## don't connect regions in different pattern group
	       ##
	       and $out[-1][2] == $top->[2]
	    ) {
	    $out[-1][1] = $top->[1] if $out[-1][1] < $top->[1];
	}
	else {
	    push @out, $top;
	}
    }
    @out;
}

sub reverse_regions {
    my $from = shift;
    my $max = shift;
    my @flat = (0, map(@$_, @$from), $max);

    grep { $_->[0] != $_->[1] }
    map  { [ splice(@flat, 0, 2) ] }
    0 .. @flat / 2 - 1;
}

sub optimized_blocknize {
    my($begin, $end) = (-1, -1);
    if ($opt_p or $opt_A or $opt_B) {
	\&blocknize;
    } else {
	sub {
	    if ($begin <= $_[1] and $_[2] < $end) {
		;
	    } else {
		($begin, $end) = &blocknize;
	    }
	    ($begin, $end);
	}
    }
}

sub blocknize {
    local *_ = shift;		# text
    my($from, $to) = @_;	# range
    my $matched = substr($_, $from, $to - $from);

    my $delim = $opt_p ? "\n\n" : "\n";

    my($begin, $end);

    for (my $c = $opt_B + 1; $c; $c--) {
	$begin = rindex($_, $delim, $from);
	last if $begin < 0;
	$from = $begin - length $delim;
    }
    if ($begin < 0) {
	$begin = 0;
    } else {
	$begin += length $delim;
    }

    if ($matched =~ /$delim\Z/) {
	$to -= length($delim);
    }
    for (my $c = $opt_A + 1; $c; $c--) {
	$end = index($_, $delim, $to);
	last if $end < 0;
	$to = $end + length $delim;
    }
    if ($end < 0) {
	$end = length $_;
    } else {
	$end += length $delim;
    }

    ($begin, $end);
}

######################################################################

sub callfunc {
    my($name, @arg) = @_;
    no strict 'refs';
    $name->(@arg);
}

##
## sub { ... }
## funcname(arg1,arg2,arg3=val3)
## funcname=arg1,arg2,arg3=val3
##
sub parse_func {
    local $_ = shift;
    my $noinline = @_ ? shift : 0;

    if (not $noinline and /^sub\s*{/) {
	my $sub = eval $_;
	if ($@) {
	    warn "Error in function -- $_ --.\n";
	    die $@;
	}
	if (ref $sub eq 'CODE') {
	    [ $sub ];
	} else {
	    die "Unexpected result from eval.\n";
	}
    }
    elsif (m{
	^ (?<name>[\w:]+)
	  (?:
	    (?: (?<P>[(]) | = )  ## start with '(' or '='
	    (?<arg> [^)]* )      ## optional arg list
	    (?(<P>) [)] | )      ## close ')' or none
	  )?
	$
    }x) {
	[ $+{name}, arg2kvlist($+{arg}) ];
    }
    else {
	undef;
    }
}

##
## convert "key1,key2,key3=val3" to (key1=>1, key2=>1, key3=>"val3")
##
sub arg2kvlist {
    map  { /=/ ? split(/=/, $_, 2) : ($_, 1) }
    map  { split /, */ }
    grep { defined }
    @_;
}

sub do_eval {
    my($prog, $debug) = @_;
    if ($debug || $opt_d{e}) {
	print STDERR &unctrl($prog), "\n" x ($prog !~ /\n$/);
    }
    eval $prog;
    if ($@) {
	die sprintf("eval failed in file %s on line %s\n$@",
		    (caller)[1, 2]);
    }
}

sub unctrl {
    local($_) = shift;
    s/([\000-\010\013-\037])/'^' . pack('c', ord($1)|0100)/ge;
    $_;
}

sub uniq {
    my %seen;
    grep { not $seen{$_}++ } @_;
}

######################################################################
package RcData;

use strict;
use warnings;

use Text::ParseWords qw(shellwords);
use List::Util qw(first);
use Data::Dumper;

sub new {
    my $class = shift;
    my $title = shift || 'main';
    bless {
	Title => $title,
	Define => [],
	Option => [],
	Help => [],
    }, $class;
}

sub title {
    my $obj = shift;
    @_  ? $obj->{Title} = shift
	: $obj->{Title};
}

sub define {
    my $obj = shift;
    my $name = shift;
    my $list = $obj->{Define};
    if (@_) {
	my $re = qr/\Q$name/;
	unshift(@$list, [ $name, $re, shift ]);
    } else {
	first { $_->[0] eq $name } @$list;
    }
}

sub expand {
    my $obj = shift;
    local *_ = shift;
    for my $defent (@{$obj->{Define}}) {
	my($name, $re, $string) = @$defent;
	s/$re/$string/g;
    }
    s/(\$ENV\{ (['"]?) \w+ \g{-1} \})/$1/xgee;
}

sub setopt {
    my $obj = shift;
    my $name = shift;
    my $list = $obj->{Option};
    my @args = shellwords(shift);
    for (@args) {
	$obj->expand(\$_);
    }
    unshift @$list, [ $name, @args ];
}

sub getopt {
    my $obj = shift;
    my($name, %opt) = @_;
    return () if $name eq 'default' and not $opt{DEFAULT};

    my $list = $obj->{Option};
    my $e = first { $_->[0] eq $name } @$list;
    my @e = $e ? @$e : ();
    shift @e;
    @e;
}

sub default {
    my $obj = shift;
    $obj->getopt('default', DEFAULT => 1);
}

sub options {
    my $obj = shift;
    reverse map { $_->[0] } @{$obj->{Option}};
}

sub help {
    my $obj = shift;
    my $name = shift;
    my $list = $obj->{Help};
    if (@_) {
	unshift(@$list, [ $name, shift ]);
    } else {
	my $e = first { $_->[0] eq $name } @$list;
	$e ? $e->[1] : undef;
    }
}

sub parsetext {
    my $obj = shift;
    my $text = shift;
    while ($text =~ /(.+)\n?/g) {
	$obj->parseline($1);
    }
    $obj;
}

sub parseline {
    my $obj = shift;
    my $line = shift;
    my($arg0, $arg1, $rest) = split(' ', $line, 3);

    if ($arg0 eq "define") {
	$obj->define($arg1, $rest);
    }
    elsif ($arg0 eq "option") {
	$obj->setopt($arg1, $rest);
    }
    elsif ($arg0 eq "defopt") {
	$obj->define($arg1, $rest);
	$obj->setopt($arg1, $arg1);
    }
    elsif ($arg0 eq "help") {
	$obj->help($arg1, $rest);
    }
#    elsif ($arg0 eq "set" and $arg1 eq "option") {
#	package main;
#	my @args = shellwords($rest);
#	GetOptionsFromArray(\@args, @optargs) || pod2usage;
#    }
    else {
	warn "$arg0: Unknown operator in rc file.\n";
    }

    $obj;
}

1;

######################################################################
package PgpFilter;
use strict;

sub new {
    my $obj = bless {
	FH        => undef,
    }, shift;
    $obj;
}

sub initialize {
    my $obj = shift;
    my($opt) = @_;
    my $passphrase;

    if (my $fd = $opt->{passphrase_fd}) {
	$obj->fh(_openfh($fd));
    }
    else {
	if (not defined $obj->fh) {
	    $obj->fh(_openfh());
	}
	if (defined $obj->{passphrase}) {
	    $passphrase = $obj->{passphrase};
	} else {
	    _readphrase(\$passphrase);
	}
	$obj->setphrase(\$passphrase);

	##
	## Destroy data as much as possible
	##
	$passphrase =~ s/./\0/g;
	$passphrase = "";
	undef $passphrase;
    }

    $obj;
}

sub setphrase {
    my $obj = shift;
    my $fh = $obj->fh;
    my($passphrase_r) = @_;

    $obj->reset;
    $fh->syswrite($$passphrase_r, length($$passphrase_r));
    $obj->reset;
}

sub fh {
    my $obj = shift;
    @_ ? $obj->{FH} = shift
       : $obj->{FH};
}

sub pgppassfd {
    my $obj = shift;
    $obj->fh->fileno;
}

sub _decrypt_command {
    my $obj = shift;
    my $command = "gpg";
    my @option = ();
    push(@option, qw(--quiet --batch --decrypt));
    push(@option, qw(--no-tty --no-mdc-warning));
    sprintf("$command @option --passphrase-fd %d", $obj->pgppassfd);
}

sub file_viewer {
    my $obj = shift;
    sprintf("pgpcat --passphrase-fd %d", $obj->pgppassfd);
}

sub reset {
    my $obj = shift;
    $obj->fh->sysseek(0, 0) or die;
}

sub _openfh {
    use Fcntl;
    use IO::File;

    my($fd) = @_;
    my $fh;

    if (defined $fd) {
	$fh = new IO::Handle;
	$fh->fdopen($fd, "w+");
    } else {
	$fh = new_tmpfile IO::File;
	defined $fh or die "new_tmpefile: $!";
    }

    $fh->fcntl(F_SETFD, 0) or die "fcntl F_SETFD failed: $!\n";

    return $fh;
}

sub _readphrase {
    my($passphrase_r) = @_;

    print STDERR "Enter PGP Passphrase> ";
    system "stty -echo";
    $$passphrase_r = <STDIN>;
    system "stty echo";
    chomp($$passphrase_r);
    print STDERR "\n";

    $passphrase_r;
}

sub decrypt {
    use IPC::Open2;

    my $obj = shift;

    my($enc_data) = @_;

    $obj->reset;

    my $pid = open2(\*RDRFH, \*WTRFH, $pgp->_decrypt_command);

    if (length($enc_data) <= 1024 * 16) {
	print WTRFH $enc_data;
    }
    else {
	## NO ERROR CHECK! XXX
	if (fork == 0) {
	    print WTRFH $enc_data;
	    close WTRFH;
	    close RDRFH;
	    exit;
	}
    }
    close WTRFH;
    my $dec_data = do { local $/ ; <RDRFH> };
    close RDRFH;

    $dec_data;
}

1;

######################################################################


=head1 DESCRIPTION


=head2 MULTIPLE KEYWORDS


B<greple> has almost the same function as Unix command L<egrep(1)> but
the search is done in the manner similar to search engine.  For
example, next command print lines those contain all of `foo' and `bar'
and `baz'.

    greple 'foo bar baz' ...

Each word can be found in any order and/or any place in the string.
So this command find all of following texts.

    foo bar baz
    baz bar foo
    the foo, bar and baz

If you want to use OR syntax, prepend question (`?') mark on each
token, or use regular expression.

    greple 'foo bar baz ?yabba ?dabba ?doo'
    greple 'foo bar baz yabba|dabba|doo'

This command will print the line which contains all of `foo', `bar'
and `baz' and one or more from `yabba', `dabba' or `doo'.

NOT operator can be specified by prefixing the token by minus (`-')
sign.  Next example will show the line which contain both `foo' and
bar' but none of `yabba' or `dabba' or `doo'.  It is ok to put `+'
mark for positive matching pattern.

    greple 'foo bar -yabba -dabba -doo'
    greple '+foo +bar -yabba|dabba|doo'

This can be written as this using B<-e> and B<-v> option.

    greple -e foo -e bar -v yabba -v dabba -v doo
    greple -e foo -e bar -v 'yabba|dabba|doo'


=head2 LINE ACROSS MATCH


B<greple> also search the pattern across the line boundaries.  This is
especially useful to handle Asian multi-byte text.  Japanese text can
be separated by newline almost any place of the text.  So the search
pattern may spread out on multiple lines.

As for ascii text, space character in the pattern matches any kind of
space including newline.  Next example will search the word sequence
of `foo', `bar' and 'baz', even they spread out to multiple lines.

    greple -e 'foo bar baz'

Option B<-e> is necessary because space is taken as a token separator
in the bare or B<--le> pattern.


=head1 OPTIONS


=head2 PATTERNS


If specific option is not provided, B<greple> takes the first argument
as a search pattern specified by B<-le> option.  All of these patterns
can be specified multiple times.

Command itself is written in Perl, and any kind of Perl style regular
expression can be used in patterns.

=over 7

=item B<--le>=I<pattern>

Treat the string as a collection of tokens separated by spaces.  Each
token is interpreted by the first character.  Token start with `-'
means negative pattern, `?' means alternative, optional `+' and
anything other means positive match.

Next example print lines which contains `foo' and `bar', and one or
more of `yabba' and 'dabba', and none of `bar' and `doo'.

    greple --le='foo bar -baz ?yabba ?dabba -doo'

Multiple `?' preceded tokens are treated all mixed together.  That
means `?A|B ?C|D' is equivalent to `?A|B|C|D'.  If you want to mean
`(A or B) and (C or D)', use AND syntax instead: `A|B C|D'.

=item B<-e> I<pattern>, B<--and>=I<pattern>

Specify positive match token.  Next two commands are equivalent.

    greple 'foo bar baz'
    greple -e foo -e bar -e baz

First character is not interpreted, so next commands will search the
pattern `-baz'.

    greple -e -baz

Space characters are treated specially by B<-e> and B<-v> options.
They are replaced by the pattern which matches any number of
white spaces including newline.  So the pattern can be expand to
multiple lines.  Next commands search the series of word `foo', `bar'
and `baz' even if they are separated by newlines.

    greple -e 'foo bar baz'

=item B<-v> I<pattern>, B<--not>=I<pattern>

Specify negative match token.  Because it does not affect to the bare
pattern argument, you can narrow down the search result like this.

    greple foo pattern file
    greple foo pattern file -v bar
    greple foo pattern file -v bar -v baz

=item B<--re>=I<pattern>

Specify regular expression.  No special treatment for space and wide
characters.

=item B<--fe>=I<pattern>

Specify fixed string pattern, like fgrep.

=item B<-i>, B<--ignore-case>

Ignore case.

=item B<--need>=I<n>

=item B<--allow>=I<n>

Option to compromize matching condition.  Option B<--need> specifies
the required match count, and B<--allow> the number of negative
condition to be overlooked.

    greple --need=2 --allow=1 'foo bar baz -yabba -dabba -doo'

Above command prints the line which contains two or more from `foo',
`bar' and `baz', and does not include more than one of `yabba',
`dabba' or `doo'.

Using option B<--need>=I<1>, B<greple> produces same result as B<grep>
command.

    grep -e foo -e bar -e baz
    greple --need=1 -e foo -e bar -e baz

When the count I<n> is negative value, it is subtracted from maximum
value.

=item B<-f> I<file>, B<--file>=I<file>

Specify the file which contains search pattern.  When file contains
multiple lines, patterns on each lines are search in OR context.

Blank line and the line starting with sharp (#) character is ignored.
Two slashes (//) and following string are taken as a comment and
removed with preceding spaces.

Multiple file can be specified, but they will be mixed into single
pattern.

=back


=head2 STYLES


=over 7

=item B<-l>

List filename only.

=item B<-c>, B<--count>

Print count of matched block.

=item B<-n>, B<--line-number>

Show line number.

=item B<-h>, B<--no-filename>

Do not display filename.

=item B<-H>

Display filename always.

=item B<-o>

Print matched string only.

=item B<-A>[I<n>], B<--after-context>[=I<n>]

=item B<-B>[I<n>], B<--before-context>[=I<n>]

=item B<-C>[I<n>], B<--context>[=I<n>]

Print I<n>-blocks before/after matched string.  The value I<n> can be
omitted and the default is 2.  When used with B<--paragraph> or
B<--block> option, I<n> means number of paragraph or block.

Actually, these options expand the area of logical operation.  It
means

    grep -C1 'foo bar baz'

matches following text.

    foo
    bar
    baz

Moreover

    greple -C1 'foo baz'

also matches this text, because matching blocks around `foo' and `bar'
overlaps each other and makes single block.

=item B<--join>

=item B<--joinby>=I<string>

Convert newline character found in matched string to empty or specifed
I<string>.  Using B<--join> with B<-o> (only-matching) option, you can
collect searching sentence list in one per line form.  This is almost
useless for English text but sometimes useful for Japanese text.  For
example, next command prints the list of KATAKANA words.

    greple -ho --join '\p{utf8::InKatakana}[\n\p{utf8::InKatakana}]*'

=item B<--filestyle>=I<line>|I<once>|I<separate>, B<--fs>

Default style is I<line>, and B<greple> prints filename at the
beginning of each line.  Style I<once> prints the filename only once
at the first time.  Style I<separate> prints filename in the separate
line before each line or block.

=item B<--linestyle>=I<line>|I<separate>, B<--ls>

Default style is I<line>, and B<greple> prints line numbers at the
beginning of each line.  Style I<separate> prints line number in the
separate line before each line or block.

=item B<--separate>

Shortcut for B<--filestyle>=I<separate> B<--linestyle>=I<separate>.
This is convenient to use block mode search and visiting each location
from supporting tool, such as Emacs.

=back


=head2 FILES


=over 7

=item B<--glob>=I<pattern>

Get files matches to specified pattern and use them as a target files.
Using B<--chdir> and B<--glob> makes easy to use B<greple> for fixed
common job.

=item B<--chdir>=I<directory>

Change directory before processing files.  When multiple directories
are specified in B<--chdir> option, by using wildcard form or
repeating option, B<--glob> file expantion will be done for every
directories.

    greple --chdir '/usr/man/man?' --glob '*.[0-9]' ...

=item B<--readlist>

Get filenames from standard input.  Read standard input and use each
line as a filename for searching.  You can feed the output from other
command like L<find(1)> for B<greple> with this option.  Next example
searches string from files modified within 7 days:

    find . -mtime -7 -print | greple --readlist pattern

=back


=head2 COLORS


=over 7

=item B<--color>=I<auto>|I<always>|I<never>, B<--nocolor>

Use terminal color capability to emphasize the matched text.  Default
is `auto': effective when STDOUT is a terminal and option B<-o> is not
given, not otherwise.  Option value `always' and `never' will work as
expected.

Option B<--nocolor> is alias for B<--color>=I<never>.

=item B<--colormode>=I<RGBCYMKWrgbcymkwSUDF>, B<--quote>=I<start>,I<end>

Specify color mode.  Default is RD: RED and BOLD.

COLOR is combination of single character representing uppercase
foreground color :

    R  Red
    G  Green
    B  Blue
    C  Cyan
    M  Magenta
    Y  Yellow
    K  Black
    W  White

and corresponding lowercase background color :

    r, g, b, c, m, y, k, w

and other effects :

    S  Standout (reverse video)
    U  Underline
    D  Double-struck (boldface)
    F  Flash (blink)

If the mode string contains comma `,' character, they are used to
quote the matched string.  If you want to quote the pattern by angle
bracket, use like this.

    greple --quote='<,>' pattern

Option B<--quote> is an alias for B<--colormode>, but it set the
option B<--color>=I<always> at the same time.

Multiple colors can be specified separating by white spaces.  Those
colors will be applied for each pattern keywords.  Next command will
show word `foo' in red, `bar' in green and `baz' in blue.

    greple --colormode='R G B' 'foo bar baz' ...

=item B<--colorful>

Shortcut for B<--colormode>='I<RD GD BD CD MD YD>'

=item B<--regioncolor>

Use different colors for each B<--inside>/B<outside> regions.

=back


=head2 BLOCKS


=over 7

=item B<-p>, B<--paragraph>

Print the paragraph which contains the pattern.  Each paragraph is
delimited by two or more successive newline characters by default.  Be
aware that an empty line is not paragraph delimiter if which contains
space characters.  Example:

    greple -np 'setuid script' /usr/man/catl/perl.l

    greple -pe '^struct sockaddr' /usr/include/sys/socket.h

It changes the unit of context specified by B<-A>, B<-B>, B<-C>
options.

=item B<--all>

Treat entire file contents as a single block.  This is almost
identical to following command.

    greple --block='(?s).*'

=item B<--block>=I<pattern>, B<--block>=I<&sub>

Specify the record block to display.  Default block is a single line.

Next example behave almost same as B<--paragraph> option, but is less
efficient.

    greple --block='(.+\n)+'

Next command treat the data as a series of 10-line blocks.

    greple -n --block='(.*\n){1,10}'

When blocks are not continuous and there are gaps between them, the
match occurred outside blocks are ignored.

If multiple block options are supplied, overlapping blocks are merged
into single block.

Please be aware that this option is sometimes quite time consuming,
because it finds all blocks before processing.

=item B<--blockend>=I<string>

Change the end mark displayed after B<-pABC> or B<--block> options.
Default value is "--\n".

=back


=head2 REGIONS


=over 7

=item B<--inside>=I<pattern>

=item B<--outside>=I<pattern>

Option B<--inside> and B<--outside> limit the text area to be matched.
For simple example, if you want to find string `and' not in the word
`command', it can be done like this.

    greple --outside=command and

The block can be larger and expand to multiple lines.  Next command
searches from C source, excluding comment part.

    greple --outside '(?s)/\*.*?\*/'

Next command searches only from POD part of the perl script.

    greple --inside='(?s)^=.*?(^=cut|\Z)'

When multiple B<inside> and B<outside> regions are specified, those
regions are mixed up in union way.

In multiple color environment, and if single keyword is specified,
matches in each B<--inside>/B<outside> regions are printed in
different colors.  Forcing this operation with multiple keywords, use
B<--regioncolor> option.

=item B<--inside>=I<&function>

=item B<--outside>=I<&function>

If the pattern name begins by ampersand (&) character, it is treated
as a name of subroutine which returns a list of blocks.  Using this
option, user can use arbitrary function to determine from what part of
the text they want to search.  User defined function can be defined in
F<.greplerc> file or by module option.

=item B<--include>=I<pattern>

=item B<--exclude>=I<pattern>

=item B<--include>=I<&function>

=item B<--exclude>=I<&function>

B<--include>/B<exclude> option behave exactly same as
B<--inside>/B<outside> when used alone.

When used in combination, B<--include>/B<exclude> are mixed in AND
manner, while B<--inside>/B<outside> are in OR.

Thus, in the next example, first line prints all matches, and second
does none.

    greple --inside PATTERN --outside PATTERN

    greple --include PATTERN --exclude PATTERN

You can make up desired matches using B<--inside>/B<outside> option,
then remove unnecessary part by B<--include>/B<exclude>

=item B<--strict>

Limit the match area strictly.

By default, B<--block>, B<--inside>/B<outside>,
B<--include>/B<exclude> option allows partial match within the
specified area.  For instance,

    greple --inside and command

matches pattern C<command> because the part of matched string is
included in specified inside-area.  Partial match failes when option
B<--strict> provided, and longer string never matches within shorter
area.

=back


=head2 CHARACTER CODE


=over 7

=item B<--icode>=I<code>

Target file is assumed to be encoded in utf8 by default.  Use this
option to set specific encoding.  When handling Japanese text, you may
choose from 7bit-jis (jis), euc-jp or shiftjis (sjis).  Multiple code
can be supplied using multiple option or combined code names with
space or comma, then file encoding is guessed from those code sets.
Use encoding name `guess' for automatic recognition from default code
list which is euc-jp and 7bit-jis.  Following commands are all
equivalent.

    greple --icode=guess ...
    greple --icode=euc-jp,7bit-jis ...
    greple --icode=euc-jp --icode=7bit-jis ...

Default code set are always included suspect code list.  If you have
just one code adding to suspect list, put + mark before the code name.
Next example does automatic code detection from euc-kr, ascii, utf8
and UTF-16/32.

    greple --icode=+euc-kr ...

=item B<--ocode>=I<code>

Specify output code.  Default is utf8.

=back


=head2 FILTER


=over 7

=item B<--if>=I<filter>, B<--if>=I<EXP>:I<filter>:I<EXP>:I<filter>:...

You can specify filter command which is applied to each files before
search.  If only one filter command is specified, it is applied to all
files.  If filter information include multiple fields separated by
colons, first field will be perl expression to check the filename
saved in variable $_.  If it successes, next filter command is pushed.
These expression and command list can be repeated.

    greple --if=rev perg
    greple --if='/\.tar$/:tar tvf -'

If the command doesn't accept standard input as processing data, you
may be able to use special device:

    greple --if='nm /dev/stdin' crypt /usr/lib/lib*

Filters for compressed and gzipped file is set by default unless
B<--noif> option is given.  Default action is like this:

    greple --if='s/\.Z$//:zcat:s/\.g?z$//:gunzip -c'

=item B<--noif>

Disable default input filter.  Which means compressed files will not
be decompressed automatically.

=item B<--of>=I<filter>

Specify output filter commands.

=back


=head2 RUNTIME FUNCTIONS


=over 7

=item B<--print>=I<function>, B<--print>=I<sub{...}>

Specify user defined function executed before data print.  Text to be
printed is replaced by the result of the funcion.  Arbitrary function
can be defined in F<.greplerc> file.  Matched data is placed in
variable C<$_>.  Other information is passed by key-value pair in the
arguments.  Filename is passed by C<file> key.  Matched informaiton is
passed by C<matched> key, in the form of perl array reference:
C<[[start,end],[start,end]...]>.

Simplest function is B<--print>='I<sub{$_}>'.  Coloring capability can
be used like this:

    # ~/.greplerc
    __CODE__
    sub print_simple {
        my %attr = @_;
        for my $r (reverse @{$attr{matched}}) {
            my($s, $e) = @$r;
            substr($_, $s, $e - $s) = color(substr($_, $s, $e - $s));
        }
        $_;
    }

Then, you can use this function in the command line.

    greple --print=print_simple ...

It is possible to use multiple B<--print> options.  In that case,
second function will get the result of the first function.  The
command will print the final result of the last funciton.

=item B<--continue>

When B<--print> option is given, B<greple> will immediately print the
result returned from print function and finish the cycle.  Option
B<--continue> forces to continue normal printing process after print
function called.  So please be sure that all data being consistent.

=item B<--call>=I<function>(I<...>), B<--call>=I<function>=I<...>

=item B<-M>I<module>::I<function(...)>, B<-M>I<module>::I<function=...>

Option B<--call> specify the function executed at the beginning of
each file processing.  This I<function> have to be called from B<main>
package.  So if you define the function in the module package, use the
full package name or export properly.

It can be set with module option, following module name.  In this
form, the function will be called with module package name.  So you
don't have to export it.

=back

For these run-time functions, optional argument list can be set in the
form of C<key> or C<key=value>, connected by comma.  These arguments
will be passed to the funciton in key => value list.  Sole key will
have the value one.  Also processing file name is passed with "file"
key.  As a result, the option in the next form:

    --print|call function(key1,key=val2)
    --print|call function=key1,key=val2

    -Mmodule::function(key1,key=val2)
    -Mmodule::function=key1,key=val2

will be transformed into following function call:

    function(file => "filename", key1 => 1, key2 => "val2")

The function can be defined in F<.greplerc> or modules.  Assign the
arguments into hash, then you can access argument list as member of
the hash.  Content of the target file can be accessed by C<$_>.

    sub function {
	my %arg = @_;
	$arg{file};    # "filename"
	$arg{key1};    # 1
	$arg{key2};    # "val2"
	$_;            # contents
    }


=head2 PGP


=over 7

=item B<--pgp>

Invoke PGP decrypt command for files end with I<.pgp>, I<.gpg> or
I<.asc>.  PGP passphrase is asked only once at the beginning of
command execution.

=item B<--pgppass>=I<phrase>

You can specify PGP passphrase by this option.  Generally, it is not
recommended to use.

=back


=head2 OTHERS


=over 7

=item B<--norc>

Do not read startup file: F<~/.greplerc>.

=item B<--man>

Show manual page.
Display module's manual page when used with B<-M> option.

=item B<--show>

Show module file contents.  Use with B<-M> option.

=item B<--require>=I<filename>

Include arbitrary perl program.

=begin comment

=item B<-d> I<flags>

Display informations.  Various kind of debug, diagnostic, monitor
information can be display by giving appropriate flag to -d option.

    d: directory information
    e: eval string
    f: processing file name
    m: misc debug information
    o: option related information
    p: run `ps' command before termination (on Unix)
    s: statistic information
    u: unused options
    v: internal match information

=end comment

=back


=head1 ENVIRONMENT and STARTUP FILE


Environment variable GREPLEOPTS is used as a default options.  They
are inserted before command line options.

Before starting execution, I<greple> reads the file named F<.greplerc>
on user's home directory.  Following directives can be used.

=over 7

=item B<option> I<name> string

Argument I<name> of `option' directive is user defined option name.
The rest are processed by I<shellwords> routine defined in
Text::ParseWords module.  Be sure that this module sometimes requires
escape backslashes.

Any kind of string can be used for option name but it is not combined
with other options.

    option --fromcode --outside='(?s)\/\*.*?\*\/'
    option --fromcomment --inside='(?s)\/\*.*?\*\/'

If the option named B<default> is defined, it will be used as a
default option.

=item B<help> I<name>

If `help' directive is used for same option name, it will be printed
in usage message.  If the help message is `ignore', corresponding line
won't show up in the usage.

=item B<define> I<name> string

Directive `define' is almost same as `option', but argument is not
processed by I<shellwords> and treated just a simple text.
Metacharacters can be included without escaping.  Defined string
replacement is done only in definition in option argument.  If you
want to use the word in command line, use option directive instead.

    define :kana: \p{utf8::InKatakana}
    option --kanalist --color=never -o --join --re ':kana:[:kana:\n]+'
    help   --kanalist List up Katakana string

=back

Environment variable substitution is done for string specified by
`option' and `define' directivies.  Use Perl syntax B<$ENV{NAME}> for
this purpose.  You can use this to make a portable module.

When I<greple> found C<__CODE__> line in F<.greplerc> file, the rest
of the file is evaluated as a Perl program.  You can define your own
subroutines which can be used by B<--inside>/B<outside>,
B<--include>/B<exclude>, B<--block> options.

For those subroutines, file content will be provided by global
variable C<$_>.  Expected response from the subroutine is the list of
array references, which is made up by start and end offset pairs.

For example, suppose that the following function is defined in your
F<.greplerc> file.  Start and end offset for each pattern match can be
taken as array element C<$-[0]> and C<$+[0]>.

    __CODE__
    sub odd_line {
        my @list;
        my $i;
        while (/.*\n/g) {
            push(@list, [ $-[0], $+[0] ]) if ++$i % 2;
        }
        @list;
    }

You can use next command to search pattern included in odd number
lines.

    % greple --inside '&odd_line' patten files...


=head1 MODULE


Modules can be specified only at the beginning of command line by
B<-M>I<module> option.  Name I<module> is prepended by B<App::Greple>,
so place the module file in F<App/Greple/> directory in Perl library.

If the package name is declared properly, C<__DATA__> section in the
module file will be interpreted same as F<.greplerc> file content.

Using B<-M> without module argument will print available module list.
Option B<--man> will display module document when used with B<-M>
option.  Use B<--show> option to see the module itself.

See this sample module code.  This sample define options to search
from pod, comment and other segment in Perl script.  Those capability
can be implemented both in function and macro.

    package App::Greple::perl;
    
    BEGIN {
        use Exporter   ();
        our ($VERSION, @ISA, @EXPORT, @EXPORT_OK, %EXPORT_TAGS);
    
        $VERSION = sprintf "%d.%03d", q$Revision: 6.20 $ =~ /(\d+)/g;
    
        @ISA         = qw(Exporter);
        @EXPORT      = qw(&pod &comment &podcomment);
        %EXPORT_TAGS = ( );
        @EXPORT_OK   = qw();
    }
    our @EXPORT_OK;
    
    END { }
    
    my $pod_re = qr{^=\w+(?s:.*?)(?:\Z|^=cut\s*\n)}m;
    my $comment_re = qr{^(?:[ \t]*#.*\n)+}m;
    
    sub pod {
        my @list;
        while (/$pod_re/g) {
            push(@list, [ $-[0], $+[0] ] );
        }
        @list;
    }
    sub comment {
        my @list;
        while (/$comment_re/g) {
            push(@list, [ $-[0], $+[0] ] );
        }
        @list;
    }
    sub podcomment {
        my @list;
        while (/$pod_re|$comment_re/g) {
            push(@list, [ $-[0], $+[0] ] );
        }
        @list;
    }
    
    1;
    
    __DATA__
    
    define :comment: ^(\s*#.*\n)+
    define :pod: ^=(?s:.*?)(?:\Z|^=cut\s*\n)
    
    #option --pod --inside :pod:
    #option --comment --inside :comment:
    #option --code --outside :pod:|:comment:
    
    option --pod --inside '&pod'
    option --comment --inside '&comment'
    option --code --outside '&podcomment'

You can use the module like this:

    greple -Mperl --pod default greple

    greple -Mperl --colorful --code --comment --pod default greple


=head1 HISTORY


Most capability of B<greple> is derived from B<mg> command, which has
been developing from early 1990's by the same author.  Because modern
standard B<grep> family command becomes to have similar capabilities,
it is a time to clean up entire functionarities, totally remodel the
option interfaces, and change the command name. (2013.11)


=head1 AUTHOR


Kazumasa Utashiro


=head1 SEE ALSO


L<grep(1)>, L<perl(1)>

L<github|http://kaz-utashiro.github.io/greple/>


=head1 LICENSE


Copyright (c) 1991-2014 Kazumasa Utashiro

Use and redistribution for ANY PURPOSE are granted as long as all
copyright notices are retained.  Redistribution with modification is
allowed provided that you make your modified version obviously
distinguishable from the original one.  THIS SOFTWARE IS PROVIDED BY
THE AUTHOR ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES ARE
DISCLAIMED.
